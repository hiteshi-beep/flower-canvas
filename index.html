<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Flower Canvas</title>

    <!-- CSS -->
    <link rel="stylesheet" href="style.css">
</head>
<body>

    <div class="container">
        <canvas id="canvas"></canvas>
        <div class="clean-btn">clean the screen</div>
    </div>

    <div class="name">Click To Add Flowers</div>

    <!-- SHADERS FIRST -->
    <script id="vertexShader" type="x-shader/x-vertex">
        varying vec2 vUv;
        void main() {
            vUv = uv;
            gl_Position = vec4(position, 1.0);
        }
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">
     #define PI 3.14159265359

uniform float u_ratio;
uniform vec2 u_cursor;
uniform float u_stop_time;
uniform float u_clean;
uniform vec2 u_stop_randomizer;

uniform sampler2D u_texture;
varying vec2 vUv;

// --------------------------------
// 2D noise

vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
vec3 permute(vec3 x) { return mod289(((x * 34.0) + 1.0) * x); }

float snoise(vec2 v) {
    const vec4 C = vec4(
        0.211324865405187,
        0.366025403784439,
        -0.577350269189626,
        0.024390243902439
    );
    vec2 i = floor(v + dot(v, C.yy));
    vec2 x0 = v - i + dot(i, C.xx);
    vec2 i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
    vec4 x12 = x0.xyxy + C.xxzz;
    x12.xy -= i1;
    i = mod289(i);
    vec3 p = permute(
        permute(i.y + vec3(0.0, i1.y, 1.0))
        + i.x + vec3(0.0, i1.x, 1.0)
    );
    vec3 m = max(
        0.5 - vec3(
            dot(x0, x0),
            dot(x12.xy, x12.xy),
            dot(x12.zw, x12.zw)
        ),
        0.0
    );
    m = m * m;
    m = m * m;
    vec3 x = 2.0 * fract(p * C.www) - 1.0;
    vec3 h = abs(x) - 0.5;
    vec3 ox = floor(x + 0.5);
    vec3 a0 = x - ox;
    m *= 1.79284291400159
         - 0.85373472095314 * (a0 * a0 + h * h);
    vec3 g;
    g.x = a0.x * x0.x + h.x * x0.y;
    g.yz = a0.yz * x12.xz + h.yz * x12.yw;
    return 130.0 * dot(m, g);
}

// --------------------------------
// Flower shape

float get_flower_shape(vec2 p, float petals, float angle, float outline) {
    angle *= 3.0;

    p = vec2(
        p.x * cos(angle) - p.y * sin(angle),
        p.x * sin(angle) + p.y * cos(angle)
    );

    float a = atan(p.y, p.x);
    float sector = pow(abs(sin(a * petals)), 0.4) + 0.25;

    float size = 0.03 + u_stop_randomizer.x * 0.1;
    float r = pow(length(p) / size, 2.0);
    r -= 0.1 * sin(8.0 * a);
    r = max(0.1, r);

    float grow = step(0.25, u_stop_time) * pow(u_stop_time, 0.3);
    float shape = 1.0 - smoothstep(0.0, sector, outline * r / grow);
    shape *= (1.0 - step(1.0, grow));

    return shape;
}

// --------------------------------
// Stem

float get_stem_shape(vec2 p, vec2 uv, float w, float angle) {
    w = max(0.004, w);

    float xoff = p.y * sin(angle);
    xoff *= pow(3.0 * uv.y, 2.0);
    p.x -= xoff;

    float n = 0.5 * snoise(2.0 * uv * u_stop_randomizer.x);
    n *= pow(p.y * p.y, 0.6);
    n *= pow(uv.y * uv.y, 0.3);
    p.x += n;

    float stem = smoothstep(-w, 0.0, p.x) *
                 (1.0 - smoothstep(0.0, w, p.x));

    float grow = 1.0 - smoothstep(0.0, 0.2, u_stop_time);
    stem *= smoothstep(0.0, pow(grow, 0.5), 0.03 - p.y);
    stem *= (1.0 - step(0.17, u_stop_time));

    return stem;
}

// --------------------------------
// Main

void main() {
    vec3 base = texture2D(u_texture, vUv).rgb;

    vec2 uv = vUv;
    uv.x *= u_ratio;

    vec2 cursor = vUv - u_cursor;
    cursor.x *= u_ratio;

    vec3 stemColor = vec3(0.1 + u_stop_randomizer.x * 0.6, 0.6, 0.2);
    vec3 flowerColor = vec3(
        0.6 + 0.5 * u_stop_randomizer.y,
        0.1,
        0.9 - 0.5 * u_stop_randomizer.y
    );

    float angle = 0.5 * (u_stop_randomizer.x - 0.5);

    float stem = get_stem_shape(cursor, uv, 0.003, angle);
    float back = get_flower_shape(cursor, 2.0, angle, 1.5);
    float front = get_flower_shape(cursor, 3.0, angle, 1.0);

    vec3 color = base;
    color += stem * stemColor;
    color += back * flowerColor;
    color += front * flowerColor;

    color *= u_clean;

    gl_FragColor = vec4(color, 1.0);
}
   
    </script>

    <!-- JS MUST BE LAST -->
    <script type="module" src="main.js"></script>

</body>
</html>
